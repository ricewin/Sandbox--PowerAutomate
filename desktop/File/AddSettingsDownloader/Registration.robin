
FUNCTION Registration GLOBAL
    XML.ReadFromFile File: $'''%OneDrive%%MasterFile%''' Encoding: XML.FileEncoding.DefaultEncoding XmlDocument=> XmlDocument
    ON ERROR DirectoryNotFoundError
        SET ErrorMsg TO $'''ディレクトリが見つかりません。'''
        CALL Catch
        THROW ERROR
    ON ERROR FileNotFoundError
        SET ErrorMsg TO $'''ファイルが見つかりません。'''
        CALL Catch
        THROW ERROR
    ON ERROR ReadFileError
        SET ErrorMsg TO $'''ファイルを読み取れませんでした。'''
        CALL Catch
        THROW ERROR
    ON ERROR InvalidFileError
        SET ErrorMsg TO $'''ファイルに有効な XML ドキュメントが含まれていません。'''
        CALL Catch
        THROW ERROR
    END
    Cryptography.HashText HashAlgorithm: Cryptography.HashAlgorithm.SHA256 Encoding: Cryptography.EncryptionEncoding.Unicode TextToHash: XmlDocument HashedText=> HashedTextA
    XML.GetXmlElementAttribute.GetElementAttributeAsNumeric Document: XmlDocument XPathQuery: $'''//Filter[last()]''' AttributeName: $'''priority''' NumericValue=> Priority
    LOOP FOREACH タイトル IN 作品リスト
        CALL Now
        XML.ExecuteXPathQuery.ExecuteXPath XmlDocument: XmlDocument XPathQuery: $'''//Filter[contains(@text, \"%タイトル%\")]''' XPathResults=> XPathResults
        IF IsNotEmpty(XPathResults) THEN
            Text.AppendLine Text: Log LineToAppend: $'''%CurrentDateTime% INFO Skip %タイトル%''' Result=> Log
            NEXT LOOP
        END
        Text.AppendLine Text: Log LineToAppend: $'''%CurrentDateTime% INFO Set %タイトル%''' Result=> Log
        **REGION 定義ファイルに追加
        Variables.IncreaseVariable Value: Priority IncrementValue: 1
        XML.InsertElement Document: XmlDocument XPathQuery: $'''Filters''' Element: $'''<Filter priority=\"%Priority%\" type=\"Regex\" hitAction=\"Filtering\" text=\".*%タイトル%.*\"/>'''
        **ENDREGION
    END
    Cryptography.HashText HashAlgorithm: Cryptography.HashAlgorithm.SHA256 Encoding: Cryptography.EncryptionEncoding.Unicode TextToHash: XmlDocument HashedText=> HashedTextB
    IF HashedTextA <> HashedTextB THEN
        XML.WriteXmlToFile.WriteToFileFormatted File: $'''%OneDrive%%MasterFile%''' Xml: XmlDocument Encoding: XML.FileEncoding.UTF8 Indentation: 2
        ON ERROR REPEAT 3 TIMES WAIT 15
        END
    END
END FUNCTION
